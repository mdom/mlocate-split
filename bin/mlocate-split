#!/usr/bin/perl

use strict;
use warnings;
use Getopt::Long qw(GetOptionsFromArray :config pass_through);
use Pod::Usage;
use Cwd qw( abs_path );
use File::Basename qw( dirname );
use English;

our $VERSION = '0.01';

exit main(@ARGV) if !caller;

sub main {
    ## set defaults
    my %options = (
        'set-ids'    => $UID == 0,
        'database' => "/var/lib/mlocate/mlocate.db",
    );

    GetOptionsFromArray( \@_, \%options, 'database=s', 'transform-path=s@',
        'set-ids!' )
      or die;

    umask 077;

    if ( !@_ ) {
        die "Missing prefixes to split\n";
    }

    my ( @prefixes, %output_db );

    for my $arg (@_) {
        my ( $prefix, $target ) = split_escape(':', $arg, 2 );
        if ( -d $prefix ) {
            $prefix = abs_path($prefix);
            $prefix =~ s{/$}{};
            push @prefixes, $prefix;
            if ( !defined $target ) {
                $target = "$prefix/.mlocate.db";
            }
            $output_db{$prefix} = $target;
        }
    }

    open( my $base_db, '<:bytes', $options{"database"} )
      or die "Can't open database $options{database}: $!.\n";

    my $header      = get_bytes( $base_db, 16 );
    my $config_size = unpack( "x8Nx4", $header );
    my $root        = read_string($base_db);
    my $config      = get_bytes( $base_db, $config_size );

    ## cache open file handles
    my %open_dbs;

    while ( !eof($base_db) ) {
        my ( $dir, $dir_header ) = read_directory($base_db);
        my $entries = read_dir_entries($base_db);

        for my $prefix (@prefixes) {

            ## $prefix has to be identical or under $dir
            if ( index( "$dir/", "$prefix/" ) == 0 ) {
                if ( not exists $open_dbs{"$output_db{$prefix}.tmp"} ) {
                    $open_dbs{"$output_db{$prefix}.tmp"} =
                      open_new_db( "$output_db{$prefix}.tmp",
                        $options{"set-ids"} );
                }
                my $db = $open_dbs{"$output_db{$prefix}.tmp"};

                my $transformed_dir =
                  transform_path( $dir, @{ $options{"transform-path"} } );

                if ( $dir eq $prefix ) {
                    truncate( $db, 0 );
                    print {$db} $header . $transformed_dir . "\x00" . $config;
                }

                print {$db} $dir_header . $transformed_dir . "\x00" . $entries;
            }
        }
    }
    for my $file ( values %output_db ) {
        rename "$file.tmp", $file;
    }
    return 0;
}

sub read_string {
    my ($fh) = @_;
    return read_until( $fh, "\x00" );
}

sub read_until {
    my ( $fh, $marker ) = @_;
    return do { local $/ = $marker; <$fh> };
}

sub get_bytes {
    my ( $fh, $num ) = @_;
    my $data;
    my $char = read( $fh, $data, $num );
    return $data;
}

sub open_new_db {
    my ( $db_file, $change_ids ) = @_;

    if ($change_ids) {
        my $parent_dir = dirname($db_file);
        my ( $uid, $gid ) = ( stat($parent_dir) )[ 4, 5 ];
        $EGID = $gid;
        $EUID = $uid;
    }

    open( my $db, '>>:bytes', $db_file )
      or die "Can't open database $db_file: $!.\n";

    if ($change_ids) {
        ( $EUID, $EGID ) = ( $UID, $GID );
    }
    return $db;
}

sub read_dir_entries {
    my ($fh) = shift;
    my $data = read_until( $fh, "\x02" );

    ## Now it is getting complicated: \x02 is a allowed character
    ## for files and directory names, so we need to check if we really
    ## found the end of directory entries. There should be three
    ## possibilities:

    ## 1. if $data is just one character long (and by definition, it's
    ##    \x02 ) we found an empty directory
    return $data if $data eq "\x02";

    ## 2. $data is two chars long (and these character have to match [\x00\x01]\x02)
    ##    this is a file starting with a \x02
    ##    in this case we just read until the next \x02
    ## 3. $data is longer and \x02 could occur everywhere. The
    ##    entries have to end with \x00\x02 (the end of the last string
    ##    plus \x02 for end of entries). The only case, where this would be
    ##    misleading is a file starting with an \x02. This will result
    ##    in the sequence \x00\x00\x02 (\x00 for the end of the last string,
    ##    \x00 for being a file and \x02 as first character). Every occurence
    ##    of \x00\x02 proceeded by another character has to be end of entries.

    while ( length($data) eq 2 or substr( $data, -3 ) !~ "[^\x00]\x00\x02" ) {
        $data .= read_until( $fh, "\x02" );
    }
    ## Puh!
    return $data;
}

sub read_directory {
    my $fh         = shift;
    my $dir_header = get_bytes( $fh, 16 );
    my $dir        = read_string($fh);
    chop($dir);    ## remove \x00
    return $dir, $dir_header;
}

sub split_escape {
    my ( $delim, $string, $count ) = @_;
    $string =~ s/\\$delim/\x00/g;
    my @data = split( $delim, $string, $count );
    s/\x00/$delim/g for @data;
    return @data;
}

sub transform_path {
    my ( $string, @rules ) = @_;
    for my $rule (@rules) {
        my ( $from, $to ) = split_escape( '=>', $rule, 2 );
        eval { $string =~ s/$from/$to/g };
        if ($@) {
            die "Tranformation failed: $@.\n";
        }
    }
    return $string;
}

__END__

=head1 NAME

mlocate-split - Split mlocate.db based on directory prefixes

=head1 SYNOPSIS

mlocate-split [--database=DB] [--transform-path FROM=E<gt>TO]...  prefix prefix2:db ...

=head1 OPTIONS

=over 4

=item B<--database=DB>

Database to split. The database has be in the format described by
I<mlocate.db(5)>. If not specified I</var/lib/mlocate.db> is used.

=item B<--transform-path "FROM=E<gt>TO">

Before written to its new database, paths can be be transformed. The
argument left of the arrow is the left operand to perls substition
operator and the right argument its replacement. The option can be given
multiple times and the substitions will be applied in order.

This can be useful, if the the original database indexes paths under
a differant mount point than on the client machines. Homedirectories
could be mounted on /export/home/... on the file server and on /home on
the clients. Only afer applying I</export/home=E<gt>/home> the clients would be
able to find the files at the right location.

=back

=head1 DESCRIPTION

mlocate-split reads the database and creates new databases for all
files under the specified directory prefixes. The prefix can contain a
colon. In this case the substring before the colon is the directory prefix
and the part after specifies its database file. If no file for the new
database is provided, the filename is assumed to be I<prefix/.mlocate.db>.

This script was mainly developed to help users find their files
faster. Home directories are often mounted from the fileserver, so the
local mlocate database doesn't contain their files. This resulted various
search tools to just call find over and over gain for every new search. It
would be possible to execute updatedb to also index network filesystems
but this would have been strenous and pointless as the mlocate.db on
the server has already been updated. With this script it's possible to
just split the servers mlocate.db and save a userspecific mlocate.db in
their home directory.

To use this new databases just add it to the users I<$LOCATE_PATH>.

=head1 EXAMPLE

  getent passwd | \
  perl -F: -lane 'print "/export$F[5]" if $F[5] =~ m{/home/\w+/\w+}' | \
  xargs /usr/local/bin/mlocate-split --transform-path '/export/home=>/home

=head1 SEE ALSO

L<locatedb(5)>, L<updatedb(1)>, L<locate(1)>

=head1 AUTHOR

Mario Domgoergen I<<mario@domgoergen.com>>
